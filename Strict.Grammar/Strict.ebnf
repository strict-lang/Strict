NUMBER				:= '-'? [0-9]+ ('.' [0-9]+)? ([eE] ('+' | '-')? [0-9]+)?
STRINGCHAR		:= [a-zA-Z0-9 .,;:!?+*/=<>()_^%&|~@#$'-]
STRING				:= '"' (STRINGCHAR | TAB | '\\')* '"'
LF						:= '\n'
SPACE					:= ' '
TAB 					:= '\t'
NAME					:= [A-Za-z]+
file					= member* method*

member				= ('has' | 'constant' | 'mutable') SPACE NAME ' with ' expression LF | ('has' | 'constant' | 'mutable') SPACE name_and_type (' = ' expression)? (' with ' expression)? LF
name_and_type = NAME (SPACE type)?
type					= NAME ('(' parameters ')')?
parameters		= parameter (',' SPACE parameter)*
parameter			= name_and_type (SPACE '=' SPACE expression)?
expression		= ternary_if | binary | unary | variable_or_method_call | constant | list
constant			= 'true' | 'false' | NUMBER | STRING
variable_or_method_call = reference ('(' arguments? ')')? ('.' variable_or_method_call)*
reference			= NUMBER '.' reference | call_target ('.' reference)*
call_target   = NAME | constant | list
arguments			= expression (', ' expression)*
ternary_if		= expression_without_ternary ' then ' expression ' else ' expression
binary				= expression_without_binary SPACE (binary_op_with_space | binary_op SPACE) expression_without_binary (SPACE (binary_op_with_space | binary_op SPACE) expression_without_binary)*
expression_without_binary = unary | method_reference | operator_call | variable_or_method_call | constant | list
expression_without_ternary = binary | expression_without_binary
binary_op_with_space = 'is not in ' | 'is not ' | 'is in '
binary_op			= '+' | '-' | '*' | '/' | '%' | '^' | '<=' | '<' | '>=' | '>' | '=' | 'in' | 'is' | 'and' | 'or' | 'xor' | 'to'
unary					= unary_op expression
unary_op			= '-' | 'not '
method_reference = 'to' SPACE type
operator_call = binary_op '(' arguments? ')' ('.' variable_or_method_call)*
list          = '()' ('.' variable_or_method_call)? | '(' expression (', ' expression)* ')' ('.' variable_or_method_call)?

method				= (type | binary_op ('(' parameters ')') | unary_op ('(' parameters ')')?) (SPACE type)? LF block*
block					= (TAB+ block_expression LF?)*
block_expression = if | selector_if | for | return | mutable | assignment | expression
if						= 'if ' expression LF block (TAB+ 'else' (SPACE if | LF block))?
selector_if		= 'if ' expression (' is' | ' is not') LF (TAB+ 'then ' expression LF)* (TAB+ 'else ' expression LF)?
for						= 'for ' ((reference ' in ' expression) | expression) LF block
return				= 'return ' expression
mutable				= 'mutable ' name_and_type (' = ' expression)?
assignment		= (('constant' | 'mutable' | 'let') SPACE)? assignable ' = ' expression
assignable		= NAME | variable_or_method_call
