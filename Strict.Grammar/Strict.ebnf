LETTER				:= [A-Za-z]
DIGIT					:= [0-9]
NUMBER				:= [0-9]+ ('.' [0-9]+)?
STRINGCHAR		:= [a-zA-Z0-9 .,;:!?+*/=<>()_^%&|~@#$'-]
STRING				:= '"' (STRINGCHAR | TAB | '\\')* '"'
LF						:= '\n' | '\r\n'
TAB						:= '\t'
NAME					:= [A-Za-z]+
PATH					:= NAME ('.' NAME)*
file					= (member | method | assignment)*
package				= PATH
type					= NAME ('(' variable (',' variable)* ')')?
member				= ('has' | 'constant' | 'mutable') variable ('=' expression)? ('with' expression)? LF?
variable			= NAME type?
method				= methodname ('(' parameters ')')? type? LF? block?
parameters		= parameter (',' parameter)*
parameter			= NAME type? ('=' expression)?
methodname		= NAME | binary_op | unary_op | conversion
block					= (TAB+ expression* LF?)*
expression		= if | for | return | mutable | assignment | member | selector_if
selector_if		= binary ('then' expression ('else' | ':') expression)?
binary				= unary (binary_op unary)*
unary				= unary_op unary | postfix
postfix			= primary ('.' methodcall)*
primary			= constant | variablecall | group
constant			= 'true' | 'false' | NUMBER | STRING
variablecall	= NAME ('(' arguments? ')')?
assignment		= 'constant'? 'mutable'? 'let'? NAME '=' expression
group					= '(' expression (',' expression)* ')'
return				= 'return' expression
methodcall		= methodname ('(' arguments? ')')?
arguments			= expression (',' expression)*
if						= 'if' expression LF block ('else' LF block)?
reference			= NAME
for						= 'for' (reference 'in')? expression LF block
binary_op			= '+' | '-' | '*' | '/' | '%' | '^' | '<' | '<=' | '>' | '>=' | 'in' | 'is' | 'is not' | 'is not in' | 'and' | 'or' | 'xor' | 'to'
unary_op			= '-' | 'not'
conversion		= 'from' | 'to'
mutable				= 'mutable' NAME '=' expression